group = 'com.michalkowol'
version = '0.1.0'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

defaultTasks 'run'

mainClassName = 'com.michalkowol.Boot'
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar) {
    baseName = "${project.name}-assembly"
    manifest = jar.manifest
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceSets {
    integrationTest {
        java {
            srcDirs += 'src/it/java'
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources.srcDirs += 'src/it/resources'
    }
}
configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

integrationTest {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

task stage(dependsOn: ['fatJar', 'build', 'clean'])
build.mustRunAfter clean
fatJar.mustRunAfter build
stage.dependsOn(fatJar)

jacocoTestReport {
    reports {
        csv.enabled false
        html.enabled true
        xml.enabled true
    }
    dependsOn test
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    ext.jacksonVersion = '2.8.7'
    ext.rinca = '0.6.1'

    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.1.+'

    compile 'com.sparkjava:spark-core:2.5.+'
    compile 'com.google.inject:guice:4.1.+'
    compile 'com.google.guava:guava:21.+'
    compile 'com.typesafe:config:1.3.+'
    compileOnly 'org.projectlombok:lombok:1.16.+'

    compile 'com.zaxxer:HikariCP:2.5.+'
    compile 'org.flywaydb:flyway-core:4.1.+'
    compile 'com.h2database:h2:1.4.+'
    compile 'org.postgresql:postgresql:9.4.+'

    compile "com.github.softwareberg.rinca:database:$rinca"
    compile "com.github.softwareberg.rinca:httpclient:$rinca"
    compile "com.github.softwareberg.rinca:json:$rinca"
    compile "com.github.softwareberg.rinca:xml:$rinca"

    testCompile 'junit:junit:4.+'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.+'
    testCompile 'org.mockito:mockito-core:2.+'
    integrationTestCompile 'com.ninja-squad:DbSetup:2.1.+'
    integrationTestCompile 'com.despegar:spark-test:1.1.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}
